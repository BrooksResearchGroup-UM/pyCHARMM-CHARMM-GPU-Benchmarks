* CHARMM input file for Multi-Site lambda-dynamics
* generated by hand (JZV/TJP 08/2018)
*
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!  Multi-Site Lambda Dynamics 
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

set fnex = 5.5 
set sysname = cb7
set builddir = new_lig_prep
set box = 65.530182
set temp = 298.15
!RX! set nreps = 5
set ligseg = lig
set resnum = 1

! perturbation variables
set nsites = 2
set nsubs1 = 6
set nsubs2 = 5 
set nblocks = 11

! read in biases & other variables
stream "variables.117.inp"

bomblev -2

!! Read in toppar stream file
stream @builddir/toppar.str

! Read PSF
open read unit 10 card name @builddir/start.psf
read psf  unit 10 card 

!Read Coordinate
open read unit 10 card name @builddir/start.crd
read coor unit 10 card

define site1sub1 sele -
bynum 4030 -
.or. bynum 4031 -
.or. bynum 4032 -
.or. bynum 4033 -
.or. bynum 4034 -
.or. bynum 4035 -
end
define site1sub2 sele -
bynum 4052 -
.or. bynum 4053 -
.or. bynum 4054 -
.or. bynum 4055 -
.or. bynum 4056 -
.or. bynum 4057 -
.or. bynum 4058 -
.or. bynum 4059 -
.or. bynum 4060 -
end
define site1sub3 sele -
bynum 4061 -
.or. bynum 4062 -
.or. bynum 4063 -
.or. bynum 4064 -
.or. bynum 4065 -
.or. bynum 4066 -
.or. bynum 4067 -
end
define site1sub4 sele -
bynum 4068 -
.or. bynum 4069 -
.or. bynum 4070 -
.or. bynum 4071 -
.or. bynum 4072 -
.or. bynum 4073 -
.or. bynum 4074 -
end
define site1sub5 sele -
bynum 4075 -
.or. bynum 4076 -
.or. bynum 4077 -
.or. bynum 4078 -
.or. bynum 4079 -
.or. bynum 4080 -
.or. bynum 4081 -
.or. bynum 4082 -
.or. bynum 4083 -
.or. bynum 4084 -
.or. bynum 4085 -
.or. bynum 4086 -
.or. bynum 4087 -
.or. bynum 4088 -
.or. bynum 4089 -
.or. bynum 4090 -
end
define site1sub6 sele -
bynum 4091 -
.or. bynum 4092 -
.or. bynum 4093 -
.or. bynum 4094 -
.or. bynum 4095 -
.or. bynum 4096 -
.or. bynum 4097 -
end
define site2sub1 sele -
bynum 4043 -
.or. bynum 4044 -
.or. bynum 4045 -
.or. bynum 4046 -
.or. bynum 4047 -
.or. bynum 4048 -
end
define site2sub2 sele -
bynum 4098 -
.or. bynum 4099 -
.or. bynum 4100 -
.or. bynum 4101 -
.or. bynum 4102 -
.or. bynum 4103 -
.or. bynum 4104 -
.or. bynum 4105 -
.or. bynum 4106 -
end
define site2sub3 sele -
bynum 4107 -
.or. bynum 4108 -
.or. bynum 4109 -
.or. bynum 4110 -
.or. bynum 4111 -
.or. bynum 4112 -
.or. bynum 4113 -
end
define site2sub4 sele -
bynum 4114 -
.or. bynum 4115 -
.or. bynum 4116 -
.or. bynum 4117 -
.or. bynum 4118 -
.or. bynum 4119 -
.or. bynum 4120 -
.or. bynum 4121 -
.or. bynum 4122 -
.or. bynum 4123 -
.or. bynum 4124 -
end
define site2sub5 sele -
bynum 4125 -
.or. bynum 4126 -
.or. bynum 4127 -
.or. bynum 4128 -
.or. bynum 4129 -
.or. bynum 4130 -
.or. bynum 4131 -
.or. bynum 4132 -
end

dele conn sele site1sub1 end sele site1sub2 end
dele conn sele site1sub1 end sele site1sub3 end
dele conn sele site1sub1 end sele site1sub4 end
dele conn sele site1sub1 end sele site1sub5 end
dele conn sele site1sub1 end sele site1sub6 end
dele conn sele site1sub2 end sele site1sub3 end
dele conn sele site1sub2 end sele site1sub4 end
dele conn sele site1sub2 end sele site1sub5 end
dele conn sele site1sub2 end sele site1sub6 end
dele conn sele site1sub3 end sele site1sub4 end
dele conn sele site1sub3 end sele site1sub5 end
dele conn sele site1sub3 end sele site1sub6 end
dele conn sele site1sub4 end sele site1sub5 end
dele conn sele site1sub4 end sele site1sub6 end
dele conn sele site1sub5 end sele site1sub6 end
dele conn sele site2sub1 end sele site2sub2 end
dele conn sele site2sub1 end sele site2sub3 end
dele conn sele site2sub1 end sele site2sub4 end
dele conn sele site2sub1 end sele site2sub5 end
dele conn sele site2sub2 end sele site2sub3 end
dele conn sele site2sub2 end sele site2sub4 end
dele conn sele site2sub2 end sele site2sub5 end
dele conn sele site2sub3 end sele site2sub4 end
dele conn sele site2sub3 end sele site2sub5 end
dele conn sele site2sub4 end sele site2sub5 end

print coor sele .not. init end


bomblev 0

write psf card name patch.psf
* patch psf file
*
write coor pdb card name patch.pdb
* patch pdb file
*
write coor card name patch.crd
* patch crd file
*

!! Create water box & periodic images
stream @builddir/imagefile.stream

!! Copy main coords into comp set
cons harm clear
coor copy comp

! LDBI math: for every "pair" there are 5 ldbi's
set ii = 1
set ldbibias = 0
label ldbimathloop
if @ii .le. @nsites then
   calc ldbibias = @ldbibias + ( ( @nsubs@@ii * (@nsubs@@ii - 1) / 2 ) * 5 )
   INCR ii by 1
   goto ldbimathloop
endif
set charge = ?cgtot

!! BLOCK setup
BLOCK 12
   clear
END
BLOCK 12 !RX! NREP @nreps
   call 2 sele site1sub1 end
   call 3 sele site1sub2 end
   call 4 sele site1sub3 end
   call 5 sele site1sub4 end
   call 6 sele site1sub5 end
   call 7 sele site1sub6 end
   call 8 sele site2sub1 end
   call 9 sele site2sub2 end
   call 10 sele site2sub3 end
   call 11 sele site2sub4 end
   call 12 sele site2sub5 end

   qldm theta
   lang temp @temp
   !RX! phmd ph 7
   soft on
   pmel ex

   
ldin 1 1.0  0.0  5.0  0.0  5.0
   ldin 2  0.1670 0.0  5.0  @lams1s1 5.0 !RX! NONE
   ldin 3  0.1670 0.0  5.0  @lams1s2 5.0 !RX! UNEG 7.0
   ldin 4  0.1670 0.0  5.0  @lams1s3 5.0 !RX! UPOS 7.0
   ldin 5  0.1670 0.0  5.0  @lams1s4 5.0 !RX! UNEG 7.0
   ldin 6  0.1670 0.0  5.0  @lams1s5 5.0 !RX! UNEG 7.0
   ldin 7  0.1670 0.0  5.0  @lams1s6 5.0 !RX! UNEG 7.0
   ldin 8  0.2000 0.0  5.0  @lams2s1 5.0 !RX! NONE
   ldin 9  0.2000 0.0  5.0  @lams2s2 5.0 !RX! UNEG 7.0
   ldin 10 0.2000 0.0  5.0  @lams2s3 5.0 !RX! UNEG 7.0
   ldin 11 0.2000 0.0  5.0  @lams2s4 5.0 !RX! UNEG 7.0
   ldin 12 0.2000 0.0  5.0  @lams2s5 5.0 !RX! UNEG 7.0

adex 2 3
adex 2 4
adex 2 5
adex 2 6
adex 2 7
adex 3 4
adex 3 5
adex 3 6
adex 3 7
adex 4 5
adex 4 6
adex 4 7
adex 5 6
adex 5 7
adex 6 7
adex 8 9
adex 8 10
adex 8 11
adex 8 12
adex 9 10
adex 9 11
adex 9 12
adex 10 11
adex 10 12
adex 11 12
   

   rmla bond thet dihe impr 
   !!rmla bond thet impr 
   msld  0  1  1  1  1  1  1  2  2  2  2  2  fnex 5.5
   msma
   
!   Add in biasing potentials
!   Check block.doc for functional form of these biasing potentials
   ldbi @ldbibias
   set ibias = 1
   set prevblock = 0
   set ii = 1
   label loop5
   if @ii .le. @nsites then
      set jj = 1
      label loop6
      if @jj .le. @nsubs@@{ii} then
         calc jp1 = @jj + 1 + @prevblock
         calc kk = @jj + 1
         label loop7
         if @kk .le. @nsubs@@{ii} then
            calc kp1 = @kk + 1 + @prevblock
            ldbv @ibias @jp1 @kp1 6 0.0 @cs@@{ii}s@@{jj}s@@{ii}s@@{kk} 0
            calc ibias = @ibias + 1
            ldbv @ibias @jp1 @kp1 10 -5.56 @xs@@{ii}s@@{jj}s@@{ii}s@@{kk} 0
            calc ibias = @ibias + 1
            ldbv @ibias @jp1 @kp1 8 0.017 @ss@@{ii}s@@{jj}s@@{ii}s@@{kk} 0
            calc ibias = @ibias + 1
            ldbv @ibias @kp1 @jp1 10 -5.56 @xs@@{ii}s@@{kk}s@@{ii}s@@{jj} 0
            calc ibias = @ibias + 1
            ldbv @ibias @kp1 @jp1 8 0.017 @ss@@{ii}s@@{kk}s@@{ii}s@@{jj} 0
            calc ibias = @ibias + 1
            calc kk = @kk + 1
            goto loop7
         endif
         calc jj = @jj + 1
         goto loop6
      endif
      calc prevblock = @prevblock + @nsubs@@{ii}
      calc ii = @ii + 1
      goto loop5
   endif
END

define ligbone sele ( segid LIG ) end

!!!! Energy Parameters
!!nbonds atom fswitch vdw vfswitch cdie eps 1 -
!!    cutnb 14.0 cutim 14.0 ctonnb 10.0 ctofnb 12.0
!!
!?? Do I need to neutralize this charged ligand system ??
nbonds cdie eps 1 vdw vfswitch cutnb 12.0 cutim 12.0 -
    ctonnb 9.0 ctofnb 10.0 Ewald kappa 0.320 pmEwald order 6 -
    fftx 72 ffty 72 fftz 72  !! ** Choose approximate box length **
    !! only numbers with prime factors of 2, 3, or 5 will work!

faster on

!!!! Initial Minimization with Ligand Constraints
!!energy
!!cons harm abso force 10.0 mass select ligbone show end comp
!!mini sd nstep 200 nprint 10 step 0.005
!!cons harm clear
!!
!!!! Set NOE distance restraints
!!NOE
!!   RESET
!!END
!!
!!NOE
!!   ! Attempt to restraint C1-C2 center-of-bond between ligands
!!   assign sele (atom LIGN 1 C1 .or. atom LIGN 1 C2) end sele (atom LIGP 2 C1 .or. atom LIGP 2 C2) end -
!!   kmin 50.0 rmin 0.0 kmax 50.0 rmax 3.0 fmax 2.0 rswitch 9999.0 sexp 1.0
!!   print anal
!!
!!   assign sele (atom LIGN 1 C1 .or. atom LIGN 1 C2) end sele (atom LIG3 1 C1 .or. atom LIG3 1 C2) end -
!!   kmin 50.0 rmin 0.0 kmax 50.0 rmax 3.0 fmax 2.0 rswitch 9999.0 sexp 1.0
!!   print anal
!!
!!    assign sele (atom LIGP 2 C1 .or. atom LIGP 2 C2) end sele (atom LIG3 1 C1 .or. atom LIG3 1 C2) end -
!!   kmin 50.0 rmin 0.0 kmax 50.0 rmax 3.0 fmax 2.0 rswitch 9999.0 sexp 1.0
!!   print anal
!!END
!!

!!!! Do Minimization
!!faster on
!!mini sd   nstep 200 nprint 10 step 0.005
!!!!mini abnr nstep 500 nprint 100 step 0.005 tolg 0.0001
!!cons harm clear
!!
!!!! Print out starting minimization
!!write psf card name aftermin.psf
!!* after min psf
!!*
!!write coor pdb card name aftermin.pdb
!!* after min pdb
!!*
!!write coor card name aftermin.crd
!!* after min crd
!!*
!!
!!read coor card name ../@{sysname}_prod.crd
read coor card name ../aftermin.crd

!---------------------------------------------------------------------------
! Setup of dynamics run
!---------------------------------------------------------------------------

!! GPU speed up with domdec
domdec gpu only dlb off gpuid @id NDIR 1 1 1

!RX! !! Initiate BP-REX MSLD
!RX! repd fast nrep @nreps EXLM FREQuency 1000 MSPH SPH 6.0 DPH 0.50 unit 27
!RX! 
!RX! !! Direct output of replicas to their individual files
!RX! open write form unit 28 name output_?myrep
!RX! outu 28

!! ** Shake must come after repd is called ** !!
shake fast bonh param
scalar fbeta set 1.0 sele all end


!! Heating run of 100 ps
open unit 21 write unform name dcd/@{sysname}_heat.dcd !RX!_?myrep
open unit 22 write form name res/@{sysname}_heat.res !RX!_?myrep
open unit 24 write file name res/@{sysname}_heat.lmd !RX!_?myrep
!!open unit 23 read form name ../@{sysname}_prod.res !RX!_?myrep
!RX! open write form unit 27 name rex-@{sysname}_heat.out_?myrep

set eqsteps = 5000
calc pmass = ?natom * 0.12
calc tmass = 1000
!scalar mass stat
!calc pmass = int ( ?stot  /  50.0 )
!calc tmass = @pmass * 10

goto noeq

!NVT!dynamics langevin start - 
dynamics cpt start - 
  - ! blade - 
  - ! PRMC - 
  timestep 0.00200 -    !! Timestep interval
  nstep @eqsteps -      !! Number of steps
  nprint 1000 -         !! Frequency of print
  iprfrq 1000 -         !! Frequency of calculating average/rms of energy
  nsavc 1000 -          !! Freq of writing coordinates
  nsavl 10 -            !! Freq lambda histograms
  isvfrq @eqsteps -     !! Freq of writing restart file
  iunread -1 -          !! Restart file shouldn't be read (-1)
  iuncrd 21 -           !! Write coordinates
  iunwri 22 -           !! Write restart file
  iunldm 24 -           !! Write lambda histograms
  firstt @temp -        !! Initial temp
  finalt @temp -        !! Final temp
  - !NVT! tstruc @temp -        !! Equilibriated starting structure temp
  - !NVT! tbath @temp -         !! Temp of heatbath
  hoover tbath @temp tmass @tmass reft @temp -
  pconstant pmass @pmass pref 1.0 pgamma 20.0 -
  ixtfrq 1000 -
  ichecw 0 -            !! Doesn't scale velocites to final temp
  ihtfrq 0 -            !! Freq of heating
  ieqfrq 0 -            !! Freq of scaling velocities
  iasors 1 -            !! Freq of assigning velocity while heating
  iasvel 1 -            !! Assigning velocity
  iscvel 0 -            !! Single scaling velocity (0)
  inbfrq -1 -           !! Regenerate nonbonded list as necessary
  ilbfrq 0 -            !! Freq of checking if atom in Langevin region
  imgfrq -1 -           !! Freq image update
  ntrfrq 500 -          !! Freq for stopping roation of molecule (auto done after heating)
  echeck -1             !! Max amount total energy can change per step

label noeq

!! Production run
open unit 21 write unform name dcd/@{sysname}_prod.dcd !RX!_?myrep
open unit 22 write form name res/@{sysname}_prod.res !RX!_?myrep
! open unit 23 read form name res/@{sysname}_heat.res !RX!_?myrep
open unit 24 write file name res/@{sysname}_prod.lmd !RX!_?myrep
!RX! open write form unit 27 name rex-@{sysname}.out_?myrep

!NVT!dynamics langevin restart -
dynamics cpt start - ! restart -
  - ! blade - 
  - ! PRMC - 
  timestep 0.00200 -    !! Timestep interval
  nstep @nsteps -       !! Number of steps
  nprint 1000 -         !! Frequency of print
  iprfrq 1000 -         !! Frequency of calculating average/rms of energy
  nsavc 1000 -          !! Freq of writing coordinates
  nsavl 10 -            !! Freq lambda histograms
  isvfrq @eqsteps -      !! Freq of writing restart file
  iunread -1 - ! iunread 23 -          !! Restart file shouldn't be read (-1)
  iuncrd 21 -           !! Write coordinates
  iunwri 22 -           !! Write restart file
  iunldm 24 -           !! Write lambda histograms
  firstt @temp -        !! Initial temp
  finalt @temp -        !! Final temp
  - !NVT! tstruc @temp -        !! Equilibriated starting structure temp
  - !NVT! tbath @temp -         !! Temp of heatbath
  hoover tbath @temp tmass @tmass reft @temp -
  pconstant pmass @pmass pref 1.0 pgamma 20.0 -
  ixtfrq 1000 -
  ichecw 0 -            !! Doesn't scale velocites to final temp
  ihtfrq 0 -            !! Freq of heating
  ieqfrq 0 -            !! Freq of scaling velocities
  iasors 1 -            !! Freq of assigning velocity while heating
  iasvel 1 -            !! Assigning velocity
  iscvel 0 -            !! Single scaling velocity (0)
  inbfrq -1 -           !! Regenerate nonbonded list as necessary
  ilbfrq 0 -            !! Freq of checking if atom in Langevin region
  imgfrq -1 -           !! Freq image update
  ntrfrq 500 -          !! Freq for stopping roation of molecule (auto done after heating)
  echeck -1             !! Max amount total energy can change per step

stop

NOE
   RESET
END
cons harm clear

! Write out structure after simulation
write psf card name @{sysname}_prod.psf !RX!_?myrep
* after dynamics psf
*
write coor pdb card name @{sysname}_prod.pdb !RX!_?myrep
* after dynamics pdb
*
write coor card name @{sysname}_prod.crd !RX!_?myrep
* after dynamics crd
*

stop

!RX! stop ! stop here if using replica exchange

!! Analyze l-dynamics output
open unit 51 write file name res/@{sysname}_prod.lmd
traj lamb refcom 1 print ctlo 0.8 cthi 0.99 first 51 nunit 1

stop

